apply plugin: 'com.android.application'
apply plugin: 'com.chaquo.python'  // Must come after com.android.application

android {
    compileSdkVersion 23
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "com.chaquo.python.demo"
        minSdkVersion 15
        targetSdkVersion 23

        versionName chaquopyVersion
        def verParsed = chaquopyVersion.split(/\./).collect { Integer.parseInt(it) }
        versionCode ((verParsed[0] * 1000000) + (verParsed[1] * 1000) + (verParsed[2] * 10) + 1)

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        python {
            // Enable and edit the following line if "python" is not on your PATH.
            // buildPython "C:/Python36/python.exe"
            version "2.7.10"
            pipInstall "six==1.10.0"
        }
        ndk {
            abiFilters "x86", "armeabi-v7a"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:preference-v14:23.4.0'
    compile 'junit:junit:4.12'

    testCompile 'junit:junit:4.12'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}
