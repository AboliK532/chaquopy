apply plugin: 'com.android.application'
apply plugin: 'com.chaquo.python'  // Must come after com.android.application

afterEvaluate {
    def assetsSrcDir = "src/main/assets/source"
    delete assetsSrcDir
    mkdir assetsSrcDir
    for (filename in ["src/main/python/chaquopy/demo/ui_demo.py",
                      "src/main/java/com/chaquo/python/demo/JavaDemoActivity.java"]) {
        assert file(filename).exists()
        copy {
            from filename
            into assetsSrcDir
        }
    }
}

android {
    compileSdkVersion 23
    buildToolsVersion "26.0.2"
    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 23

        def appVersion = "1.2.0"
        versionName appVersion
        def verParsed = appVersion.split(/\./).collect { Integer.parseInt(it) }
        versionCode ((verParsed[0] * 1000000) + (verParsed[1] * 1000) + (verParsed[2] * 10) + 1)

        python {
            // Enable and edit the following line if "python" is not on your PATH.
            // buildPython "C:/Python27/python.exe"

            // Android UI demo
            pip {
                install "Pygments==2.2.0"   // Also used in Java API demo
            }
            staticProxy "chaquopy.demo.ui_demo"

            // Python unit tests
            pip {
                install "MarkupSafe==1.0"
            }
            staticProxy("chaquopy.test.static_proxy.basic", "chaquopy.test.static_proxy.header",
                        "chaquopy.test.static_proxy.method")
        }
        ndk {
            abiFilters "x86", "armeabi-v7a"
        }

        // Chaquopy generates extra internal-use constructors on static proxy classes.
        lintOptions {
            disable "ValidFragment"
        }

        // Remove other languages imported from Android support libraries.
        resConfigs "en"
    }

    flavorDimensions "pyVersion"
    productFlavors {
        py2 {
            dimension "pyVersion"
            applicationId "com.chaquo.python.demo"
            python { version "2.7.14" }
        }
        py3 {
            dimension "pyVersion"
            applicationId "com.chaquo.python.demo3"
            python { version "3.6.3" }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "demo-${variant.versionName}-${variant.name}.apk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.4.0'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:preference-v14:23.4.0'
    compile 'junit:junit:4.12'
    testCompile 'junit:junit:4.12'
}
